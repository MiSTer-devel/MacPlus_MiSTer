/* Synchronous 8-bit replica of 3.5 inch floppy disk drive.

	Differences from the true floppy interace at the Mac's DB-19 port:
	True interface has a writeReq control line, only 1-bit readData and writeData, and no clk.
	True interface does not have newByteReady signal. Instead the IWM must watch the data in bit and synchronize with it to
	   determine the timing and framing of bytes.
	
*/

/* Disk register (read):	
	    State-control lines       Register
  CA2    CA1    CA0    SEL    addressed    Information in register

  0      0      0      0      DIRTN        Head step direction (0=toward track 79, 1=toward track 0)
  0      0      0      1      CSTIN        Disk in place (0=disk is inserted)
  0      0      1      0      STEP         Drive head stepping (setting to 0 performs a step, returns to 1 when step is complete)
  0      0      1      1      WRTPRT       Disk locked (0=locked)
  0      1      0      0      MOTORON      Drive motor running (0=on, 1=off)
  0      1      0      1      TKO          Head at track 0 (0=at track 0)
  0		1		 1		  0		SWITCHED   	 Disk switched (1=yes?)
  0      1      1      1      TACH         Tachometer (produces 60 pulses for each rotation of the drive motor)
  1      0      0      0      RDDATA0      Read data, lower head, side 0
  1      0      0      1      RDDATA1      Read data, upper head, side 1 
  1      0      1      0      SUPERDR      Drive is a Superdrive (0=no, 1=yes)
  1      1      0      0      SIDES        Single- or double-sided drive (0=single side, 1=double side)
  1      1      0      1      READY        0 = yes
  1      1      1      0      INSTALLED	 0 = yes
  1      1      1      1      DRVIN        400K/800K: Drive installed (0=drive is present), Superdrive: Inserted disk capacity (0=HD, 1=DD)
	
	Disk registers (write):
    Control lines      Register
  CA1    CA0    SEL    addressed    Register function

  0      0      0      DIRTN        Set stepping direction (0=toward track 79, 1=toward track 0)
  0      0      1      SWITCHED		Reset disk switched flag (writing 1 sets switch flag to 0)
  0      1      0      STEP         Step the drive head one track (setting to 0 performs a step, returns to 1 when step is complete)
  1      0      0      MOTORON      Turn on/off drive motor (0=on, 1=off)
  1      1      0      EJECT        Eject the disk (writing 1 ejects the disk)
	
*/

`define DRIVE_REG_DIRTN		0  /* R/W: step direction (0=toward track 79, 1=toward track 0) */
`define DRIVE_REG_CSTIN		1  /* R: disk in place (1 = no disk) */
	                           /* W: ?? reset disk switch flag ? */
`define DRIVE_REG_STEP		2  /* R: drive head is stepping (1 = complete) */
	                           /* W: 0 = step drive head */
`define DRIVE_REG_WRTPRT	3  /* R: 0 = disk is write-protected */
`define DRIVE_REG_MOTORON	4  /* R/W: 0 = motor on */
`define DRIVE_REG_TK0		5  /* R: 0 = head at track 0 */
`define DRIVE_REG_EJECT		6  /* R: disk switched (1=yes?)*/
	                           /* W: 1 = eject the disk */
`define DRIVE_REG_TACH		7  /* R: tach-o-meter */
`define DRIVE_REG_RDDATA0	8  /* R: activate lower head: side 0 */
`define DRIVE_REG_RDDATA1	9  /* R: activate upper head: side 1 */
`define DRIVE_REG_SUPERDR	10 /* R: drive is a superdrive (0=no, 1=yes) */
`define DRIVE_REG_SIDES		12 /* R: number of sides (0=single, 1=dbl) */
`define DRIVE_REG_READY		13 /* R: drive ready (head loaded) (0=ready) */
`define DRIVE_REG_INSTALLED	14 /* R: drive present (0 = yes ??) */
`define DRIVE_REG_DRVIN		15 /* R: 400K/800k: drive present (0=yes, 1=no), Superdrive: disk capacity (0=HD, 1=DD) */

module floppy
(
	input clk,
	input cep,
	input cen,

	input _reset,
	input ca0,				// PH0
	input ca1,				// PH1
	input ca2,				// PH2
	input SEL, 				// HDSEL from VIA
	input lstrb,			// aka PH3
	input _enable,
	input [7:0] writeData,
	output [7:0] readData,

	input advanceDriveHead,  // prevents overrun when debugging, does not exist on a real Mac!
	output reg newByteReady,
	input insertDisk,
	// the mounted file is 819,200 bytes rather than 409,600
	input img800k,
	// the drive mechanism: 1 = 800K double-sided, 0 = 400K single-sided
	input drive800k,
	// the medium's own sidedness, from floppy_loader.v's mount-time sniff
	input mediaSides,
	// spindle duty index 0..399 from rtl/disk_pwm_duty.v
	input [8:0] disk_pwm,
	output diskEject,

	output motor,
	output act,

	output [21:0] dskReadAddr,
	input dskReadAck,
	input [7:0] dskReadData,

	// floppy write path
	input writeReq,        // pulse: CPU registered a new byte in the IWM write-data register
	input writeProtect,    // 1 = writes refused for this drive (OSD toggle ANDed with img_readonly)
	output writeBusy,      // 1 = write buffer full, mac must wait (iwm.v inverts for _iwmBusy)
	output writeUnderrun,  // 1 = an in-flight write byte was abandoned (iwm.v inverts for _writeUnderrun)
	input  writeMode,      // IWM Q7: in write mode. Bounds a write for the format relay

	output [21:0] dskWriteAddr,
	output [15:0] dskWriteData,
	output        dskWriteReq,
	input         dskWriteAck,

	// SD persistence tap, mirrors the SDRAM commit for floppy_sd_writer
	output        dskCommitDone,   // one clk pulse: sector fully committed to SDRAM
	output [21:0] dskCommitAddr,   // image byte offset of sector byte 0, valid at dskCommitDone
	output        dskCommitBufWr,
	output [7:0]  dskCommitBufAddr,
	output [15:0] dskCommitBufData
);

	assign motor = ~driveRegs[`DRIVE_REG_MOTORON];
	assign act = lstrbEdge;

	reg [15:0] driveRegs;
	reg [6:0] driveTrack;
	reg driveSide;
	reg [7:0] diskDataIn; // incoming byte from the floppy disk
	
	// read drive registers
	wire [15:0] driveRegsAsRead = {
		1'b0, // DRVIN = yes
		1'b0, // INSTALLED = yes
		1'b0, // READY = yes
		// SIDES: the 128K and 512K have a single-sided drive
		drive800k, // SIDES: 1 = double-sided drive, 0 = single-sided
		1'b0, // UNUSED
		1'b0, // SUPERDR
		1'b0, // RDDATA1
		1'b0, // RDDATA0
		driveRegs[`DRIVE_REG_TACH], // TACH: 60 pules for each rotation of the drive motor
		diskSwitched, // disk switched?
		~(driveTrack == 7'h00), // TK0: track 0 indicator
		driveRegs[`DRIVE_REG_MOTORON], // motor on
		~writeProtect, // WRTPRT: 0 = locked, 1 = write enabled
		1'b1, // STEP = complete
		driveRegs[`DRIVE_REG_CSTIN], // disk in drive
		driveRegs[`DRIVE_REG_DIRTN] // step direction
	};

	reg dskReadAckD;
	always @(posedge clk) if(cen) dskReadAckD <= dskReadAck;

	// latch incoming data
	reg [7:0] dskReadDataLatch;
	always @(posedge clk) if(cep && dskReadAckD) dskReadDataLatch <= dskReadData;
		
	wire [7:0] dskReadDataEnc;
	
	reg old_newByteReady;
	always @(posedge clk) old_newByteReady <= newByteReady;
	
	// format relay signals, driven by the write path below
	wire        secAmark;
	wire [3:0]  secAmarkSector;
	wire        secFmtMark;
	wire        secFmtDs;
	reg         wrEnd;

	// include track encoder
	floppy_track_encoder enc
	(

		.clk		( clk ),
		.ready	( ~old_newByteReady & newByteReady ),

		.rst     ( !_reset ),
		
		.side    ( driveSide ),
		.sides   ( doubleSidedDisk ),
		.track   ( driveTrack ),

		.addr    ( dskReadAddr ),
		.idata   ( dskReadDataLatch ),
		.odata   ( dskReadDataEnc ),

		// format relay: the write stream as the decoder consumed it
		.wr_byte        ( decReady ),
		.wr_mark        ( secAmark ),
		.wr_mark_sector ( secAmarkSector ),
		.wr_end         ( wrEnd )
	);

	// double-sided = drive mechanism AND file size AND the medium (volume
	// header at mount, or the format byte of the last formatted track)
	reg fmtSeen; // an address field's format byte has been read since the mount
	reg fmtDs;
	always @(posedge clk) begin
		// cleared on the same eject/mount events as the decoder
		if (!_reset || writePathReset) begin
			fmtSeen <= 1'b0;
			fmtDs   <= 1'b0;
		end
		else if (secFmtMark) begin
			fmtSeen <= 1'b1;
			fmtDs   <= secFmtDs;
		end
	end

	wire doubleSidedDisk = drive800k && img800k && (fmtSeen ? fmtDs : mediaSides);

	// Write path. CPU bytes are paced at the read side's 16us byte time, then
	// handed to floppy_track_decoder; a checksum-valid sector is drained to
	// SDRAM by floppy_write_committer over the shared extra-slot-3 port.
	// writeUnderrun is raised only for a byte abandoned by deselect.
	reg        writeBusyReg;
	reg [6:0]  writeByteTimer;
	reg [7:0]  pendingWriteByte;
	reg        writeUnderrunReg;
	reg        decReady;

	assign writeBusy     = writeBusyReg;
	assign writeUnderrun = writeUnderrunReg;

	// Any disk change (OS eject or a fresh mount) resets the write path, so a
	// half-decoded field cannot complete with the next image's bytes.
	// insertDisk is a level; its rising edge is the mount event. Reset to a
	// constant 1: a non-constant async-reset value makes Quartus build a latch.
	reg insertDiskPrev;
	always @(posedge clk or negedge _reset)
		if (!_reset)   insertDiskPrev <= 1'b1;
		else if (cep)  insertDiskPrev <= insertDisk;
	wire insertDiskEdge = insertDisk && !insertDiskPrev;
	wire insertDiskFall = !insertDisk && insertDiskPrev;

	wire ejectPulse = cep && _enable == 1'b0 && lstrbEdge == 1'b1 &&
	                   driveWriteAddr == `DRIVE_REG_EJECT && ca2 == 1'b1;

	// both edges of insertDisk reset the write path: it drops at img_mounted
	// and rises at ldr_*_done
	wire writePathReset = ejectPulse || (cep && (insertDiskEdge || insertDiskFall));

	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin
			writeBusyReg     <= 1'b0;
			writeByteTimer   <= 7'd0;
			pendingWriteByte <= 8'd0;
			writeUnderrunReg <= 1'b0;
			decReady         <= 1'b0;
		end else if (writePathReset) begin
			// abandon any in-flight write byte
			writeBusyReg     <= 1'b0;
			writeByteTimer   <= 7'd0;
			decReady         <= 1'b0;
		end else begin
			decReady <= 1'b0; // default; pulsed for exactly one cep below

			// byte pacing runs on the same clk8 cadence as diskDataByteTimer
			if (cep && writeBusyReg) begin
				if (_enable == 1'b1) begin
					// drive deselected mid-byte: it never reached the media
					writeBusyReg     <= 1'b0;
					writeUnderrunReg <= 1'b1;
				end else if (writeByteTimer == 7'd127) begin
					writeBusyReg <= 1'b0;
					decReady     <= 1'b1; // hand this byte to the decoder now
				end else begin
					writeByteTimer <= writeByteTimer + 1'b1;
				end
			end

			// byte accepted when the IWM registers a write byte for this drive (cen and
			// cep never coincide). insertDisk as well as CSTIN: CSTIN is only set by an
			// OS eject and is not cleared by a remount
			if (writeReq && _enable == 1'b0 && !writeProtect && !writeBusyReg &&
			    !driveRegs[`DRIVE_REG_CSTIN] && insertDisk) begin
				pendingWriteByte <= writeData;
				writeBusyReg     <= 1'b1;
				writeByteTimer   <= 7'd0;
				writeUnderrunReg <= 1'b0;
			end
		end
	end

	// the write as a whole, for the encoder's format relay; wrEnd is delayed
	// two clocks so the encoder sees a mark before the end
	reg  wrBusyPrev, wrEndD1;
	wire wrBusy = (writeMode && _enable == 1'b0) || writeBusyReg;
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin
			wrBusyPrev <= 1'b0;
			wrEndD1    <= 1'b0;
			wrEnd      <= 1'b0;
		end else begin
			if (cep) wrBusyPrev <= wrBusy;
			wrEndD1 <= (cep && wrBusyPrev && !wrBusy) || writePathReset;
			wrEnd   <= wrEndD1;
		end
	end

	wire        secValid, secReject;
	wire [3:0]  secNum;
	wire [21:0] secAddr;
	wire [8:0]  wcBufAddr;
	wire [7:0]  wcBufData;

	floppy_track_decoder dec
	(
		.clk          ( clk ),
		.ready        ( decReady ),
		.rst          ( !_reset || writePathReset ),

		.side         ( driveSide ),
		.sides        ( doubleSidedDisk ),
		.track        ( driveTrack ),

		.idata        ( pendingWriteByte ),

		.sector_valid ( secValid ),
		.sector       ( secNum ),
		.addr         ( secAddr ),
		.reject       ( secReject ),
		.amark        ( secAmark ),
		.amark_sector ( secAmarkSector ),
		.fmt_mark     ( secFmtMark ),
		.fmt_ds       ( secFmtDs ),

		.buf_addr     ( wcBufAddr ),
		.buf_data     ( wcBufData )
	);

	floppy_write_committer wc
	(
		.clk          ( clk ),
		.rst          ( !_reset || writePathReset ),

		.sector_valid ( secValid ),
		.sector_addr  ( secAddr ),
		.buf_addr     ( wcBufAddr ),
		.buf_data     ( wcBufData ),

		.wr_addr      ( dskWriteAddr ),
		.wr_data      ( dskWriteData ),
		.wr_req       ( dskWriteReq ),
		.wr_ack       ( dskWriteAck ),

		.busy         (  ),
		.done         ( dskCommitDone ),
		.committed_addr ( dskCommitAddr ),

		.sd_buf_addr  ( dskCommitBufAddr ),
		.sd_buf_data  ( dskCommitBufData ),
		.sd_buf_wr    ( dskCommitBufWr )
	);
	
	wire [3:0] driveReadAddr = {ca2,ca1,ca0,SEL};
	
	// a byte is read or written every 128 clocks (2 us per bit * 8 bits = 16 us, @ 8 MHz = 128 clocks)
	// The CPU must poll for data at least this often, or else an overrun will occur.
	reg [6:0] diskDataByteTimer; 
	reg [7:0] diskImageData;	
	reg readyToAdvanceHead;
	always @(posedge clk or negedge _reset) begin
		if (_reset == 0) begin		
			driveSide <= 0;
			diskImageData <= 8'h00;
			diskDataIn <= 8'hFF;
			diskDataByteTimer <= 0;
			readyToAdvanceHead <= 1;
			newByteReady <= 1'b0;
		end 
		else begin			
			if(cep) begin
			// at time 0, latch a new byte and advance the drive head
			if (diskDataByteTimer == 0 && readyToAdvanceHead && diskImageData != 0) begin
				diskDataIn <= diskImageData;
				newByteReady <= 1;
				diskDataByteTimer <= 1;  // make timer run again

				// clear diskImageData after it's used, so we can tell when we get a new one from the disk	
				diskImageData <= 0;

				// for debugging, don't advance the head until the IWM says it's ready
				readyToAdvanceHead <= 1'b1; // TEMP: treat IWM as always ready
			end

			// extraRomReadAck comes every hsync which is every 21us. The iwm data rates
			// is 8MHZ/128 = 16us
			else begin
				// a timer governs when the next disk byte will become available
				diskDataByteTimer <= diskDataByteTimer + 1'b1;

				newByteReady <= 1'b0;

				if (dskReadAck) begin
					// whenever ACK is received, store the data from the current diskImageAddr 
					diskImageData <= dskReadDataEnc;  // xyz
 				end

				if (advanceDriveHead) begin
					readyToAdvanceHead <= 1'b1;
				end
			end

			// switch drive sides if DRIVE_REG_RDDATA0 or DRIVE_REG_RDDATA1 are read
			// TODO: we don't know if this is a true read, since we don't know if IWM is selected or 
			// could be bad if we use this test to flush a cache of encoded disk data
			if (driveReadAddr == `DRIVE_REG_RDDATA0 && lstrb == 1'b0)
				driveSide <= 0;
			if (driveReadAddr == `DRIVE_REG_RDDATA1 && lstrb == 1'b0)
				driveSide <= 1;	
		end
	end
	end

	// create a signal on the falling edge of lstrb
	reg lstrbPrev;
	always @(posedge clk) if(cep) lstrbPrev <= lstrb;

	wire lstrbEdge = lstrb == 1'b0 && lstrbPrev == 1'b1;

	assign readData = _enable ? 8'hFF :
	                  (driveReadAddr == `DRIVE_REG_RDDATA0 || driveReadAddr == `DRIVE_REG_RDDATA1) ? diskDataIn :
							{ driveRegsAsRead[driveReadAddr], 7'h00 };
		
	// write drive registers
	wire [2:0] driveWriteAddr = {ca1,ca0,SEL};
	
	// DRIVE_REG_DIRTN		0  /* R/W: step direction (0=toward track 79, 1=toward track 0) */
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin		
			driveRegs[`DRIVE_REG_DIRTN] <= 1'b0;
		end 
		else if(cep && _enable == 1'b0 && lstrbEdge == 1'b1 && driveWriteAddr == `DRIVE_REG_DIRTN) begin
			driveRegs[`DRIVE_REG_DIRTN] <= ca2;
		end
	end


	// DRIVE_REG_CSTIN		1  /* R: disk in place (1 = no disk) */
										/* W: ?? reset disk switch flag ? */
	// disk in drive indicators
	reg [23:0] ejectIndicatorTimer;
	assign diskEject = (ejectIndicatorTimer != 0);
	
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin		
			driveRegs[`DRIVE_REG_CSTIN] <= 1'b1;
			ejectIndicatorTimer <= 24'd0;
		end 
		else if(cep) begin
			if (_enable == 1'b0 && lstrbEdge == 1'b1 && driveWriteAddr == `DRIVE_REG_EJECT && ca2 == 1'b1) begin
				// eject the disk
				driveRegs[`DRIVE_REG_CSTIN] <= 1'b1;
				ejectIndicatorTimer <= 24'hFFFFFF;
			end
			else if (insertDisk) begin
				// insert a disk
				driveRegs[`DRIVE_REG_CSTIN] <= 1'b0;
			end
			else begin
				if (ejectIndicatorTimer != 0)
					ejectIndicatorTimer <= ejectIndicatorTimer - 1'b1;
			end
		end
	end

	// SWITCHED: set on eject or a fresh mount, cleared by the reset-disk-switched
	// register write
	reg diskSwitched;
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin
			diskSwitched <= 1'b0;
		end
		else if (ejectPulse || (cep && insertDiskEdge)) begin
			diskSwitched <= 1'b1;
		end
		else if (cep && _enable == 1'b0 && lstrbEdge == 1'b1 &&
		         driveWriteAddr == `DRIVE_REG_CSTIN && ca2 == 1'b1) begin
			diskSwitched <= 1'b0;
		end
	end

	//`define DRIVE_REG_STEP		2  /* R: drive head stepping (1 = complete) */
												/* W: 0 = step drive head */
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin	
			driveTrack <= 0; 
		end 
		else if(cep && _enable == 1'b0 && lstrbEdge == 1'b1 && driveWriteAddr == `DRIVE_REG_STEP && ca2 == 1'b0) begin
			if (driveRegs[`DRIVE_REG_DIRTN] == 1'b0 && driveTrack != 7'h4F) begin
				driveTrack <= driveTrack + 1'b1;
			end
			if (driveRegs[`DRIVE_REG_DIRTN] == 1'b1 && driveTrack != 0) begin
				driveTrack <= driveTrack - 1'b1;
			end
		end
	end
	
	// DRIVE_REG_MOTORON	4  /* R/W: 0 = motor on */
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin		
			driveRegs[`DRIVE_REG_MOTORON] <= 1'b1;
		end 
		else if (cep && _enable == 1'b0 && lstrbEdge == 1'b1 && driveWriteAddr == `DRIVE_REG_MOTORON) begin
			driveRegs[`DRIVE_REG_MOTORON] <= ca2;
		end
	end

	// DRIVE_REG_TACH  7  Tachometer (produces 60 pulses for each rotation of the drive motor)
	/* Data from MESS, sonydriv.c:
	   Tracks	RPM   Timing Value
	   00-15:   402   timing value $117B (acceptable range {1135-11E9})
	   16-31:   438   timing value $???? (acceptable range {12C6-138A})
	   32-47:   482   timing value $???? (acceptable range {14A7-157F})
	   48-63:   536   timing value $???? (acceptable range {16F2-17E2})
	   64-79:   603   timing value $???? (acceptable range {19D0-1ADE})

	   RPM per Guide to the Macintosh Family Hardware; sonydriv.c labels the
	   same rows 500/550/600/675/750.
		
		Experimentally determined toggle rates for Plus Too with 8.125 MHz CPU clock:
		TACH Half Period Clocks		Resulting Timing Value
					9996					$117B (4475)
					9122  				$1328 (4904)
					8292  				$1513 (5395)
					7463  				$176A (5994)
					6634					$1A56 (6742)
	*/
	
	reg [13:0] driveTachTimer; 
	reg [13:0] driveTachBase;
	
	always @(*) begin
		case (driveTrack[6:4])
			0: // tracks 0-15
				driveTachBase <= 9996;
			1: // tracks 16-31
				driveTachBase <= 9122;
			2: // tracks 32-47
				driveTachBase <= 8292;
			3: // tracks 48-63
				driveTachBase <= 7463;
			default: // tracks 64-79
				driveTachBase <= 6634;
		endcase
	end

	// 400K mechanism: period from the Mac's PWM duty (index 101 ~402 rpm,
	// 302 ~603 rpm); an 800K mechanism keeps the track table above
	wire [13:0] pwm_span   = {disk_pwm, 4'b0} + {5'b0, disk_pwm}; // index*17
	wire [13:0] pwm_period = 14'd11686 - pwm_span;                // 11686..4903
	wire [13:0] driveTachPeriod = drive800k ? driveTachBase : pwm_period;
	
	always @(posedge clk or negedge _reset) begin
		if (_reset == 1'b0) begin		
			driveRegs[`DRIVE_REG_TACH] <= 1'b0;
			driveTachTimer <= 0;
		end 
		else if(cep) begin
			if (driveTachTimer == driveTachPeriod) begin
				driveTachTimer <= 0;
				driveRegs[`DRIVE_REG_TACH] <= ~driveRegs[`DRIVE_REG_TACH];
			end
			else begin
				driveTachTimer <= driveTachTimer + 1'b1;
			end
		end
	end	
endmodule
